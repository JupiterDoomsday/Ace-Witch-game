title: Start
position: -192,-370
---
<<profile GameStateMachine marji lookat>> 
<<profile GameStateMachine walker default>> 
///checks to see if the user has come out as Ace
<<declare $cameOutAce = false>>
<<declare $birdBook = false>>
<<if $cameOutAce is false>>
Marji:Hey bro!
Dani: Hey.
-> I have to tell you something
    <<jump hows_day_been>> 
-> Naw
    <<jump quick_end>>
<<elseif $birdBook is false>>
-> Whatcha reading?
    <<jump Gleeb>>
-> Naw
    <<jump quick_end>>
<<else>>
Marji: Sooooo! Did you find that bird?
Dani: Not yet.
<<endif>>
===
title: quick_end
position: -551,-158
---
Dani: See you later!
===
title: hows_day_been
position: 59,-369
---
<<playAndWait GameStateMachine CrossFade fade_in>>
<<SetPosition Marji -2.3 5>>
<<SetDirection Marji RIGHT true>>
<<playAndWait GameStateMachine CrossFade fade_out>>
Dani: yeah?
Marji: Um. What'cha doin'?
Dani: I just woke up and catching up on my reading.
<<profile GameStateMachine marji smile>> 
Marji: Ha! Me too! I mean-- not the book part!
Dani: Hilarious.
<<profile GameStateMachine walker lookaway>>
<<playAndWait GameStateMachine walker book_down>>
Dani: In fact, what's even more funny is you're never up this early.
<<profile GameStateMachine marji stressed>> 
Marji:...
Dani:...
<<profile GameStateMachine walker default>> 
Dani: Okay, there's either two things happening here–– One. You need help, presumably with a spell. Two. You need help, cause you messed up a spell–
-> Pft-- I wish!
    <<jump hdb_resp1>>
-> I just want your love and support
    <<jump hbd_resp2>>
-> Nu-uh!
    <<jump hbd_resp3>>
===
title: hdb_resp1
position: 361,-154
---
Dani: Marji, if you're going to hex someone at least wake me up so I can see the trainwreck!
<<profile GameStateMachine marji smile>> 
Marji: Naw, only to my enemies.
Marji: Anyways!
-> I'm Asexual!
    <<jump comming_out>>
-> I don't like people the way others do
    <<jump comming_outt>>
===
title: hbd_resp2
position: -199,-156
---
<<profile GameStateMachine marji lookat>>
Dani: Okay, Get out!
Dani: I'm trying to finish this book before our shift starts!
Marji: Wait!
Dani: What?
<<profile GameStateMachine marji default>>
Marji: I mean it!
Dani: Right, sure you do.
Marji: For real! Its' super important and you can't tell anyone! So you better pinky swear! No fingers crossed!
Dani: ...
Dani: You didn't... summon anything ancient? Right?
<<profile GameStateMachine marji smile>>
Marji: Naw!
<<profile GameStateMachine walker hyper>>
<<profile GameStateMachine marji lookat>>
Dani: Okay, Good! You know how much I love calculating the non-eclidian geometry for summoning blueprints. 
Dani: Although, doing stellar parralax on elder stars is-
Marji: AH-HEM! 
<<profile GameStateMachine walker lookaway>>
Dani: What? I love math!
<<profile GameStateMachine marji irritated>>
Marji: Bro! Let me talk!
<<profile GameStateMachine walker default>>
Dani: No.
-> (throw your pillow at your brother)
    <<jump throw_pillow>>
-> Uh- too bad!
    <<jump taunt_bro>>
===
title: hbd_resp3
position: 110,-130
---
Dani: Okay. Sure. Then what is it?
Marji: Weeeeeeell, I'm gonna need ya to pinky swear!
Dani: Pinky swear? What is this? Baby's first summoning contract?!?
Marji: Do the damn pinky swear!
Dani: Fine.
<<showImage GameStateMachine true true>>
Dani: I pinky swear not to tell.
<<showImage GameStateMachine false false>>
Marji: Cool! Sooooooo--
-> I'm Asexual!
    <<jump comming_out>>
-> I don't like people the way others do
    <<jump comming_outt>>
===
title: comming_out
position: 421,116
---
Dani: You're what?
Marji: I–
(Marji looks away distressed)
Marji: I– uh...
Dani: Is this a joke?
(Dani turns his face away)
Dani: Heh...
Dani: Hahahahaha
Marji: No! You're a joke!
<<jump dani_out>>
===
title: comming_outt
position: 141,140
---
<<profile GameStateMachine walker lookaway>>
Dani: I mean... people get on my nerves too. I don't think its special or anything, though.
(Marji sprite shakes her head)
Marji: No! I don't "like" like people!
<<profile GameStateMachine walker default>>
Dani: Oh.
Marji: I-- uh... Don't like boys...
Dani: Okay.
Marji: AAAAND I don't like girls!
<<profile GameStateMachine walker sus>>
Dani: So what? Are you telling me you're Asexual?
Marji: Yes!
Marji: Wait-- huh!?!?
<<profile GameStateMachine walker lookaway>>
Dani: Well, well, I guess I'm not the only one in the family.
Marji: Are you for real!?!
<<profile GameStateMachine walker default>>
Dani: Yeah.
(Marji sprite flops to her side)
Marji: Uuuuuugh! I was scared for NOTHING!
Dani: Hey Marji.
<<jump the_end>>
===

title: the_end
position: 242,614
---
Marji: What?
<<profile GameStateMachine walker smile>>
Dani: I'm proud of you! Thanks for sharing this with me.
Marji: Yeah, yeah-- I love you too!
Dani: You know what–– yeah, I love you too.
<<playAndWait GameStateMachine walker book_up>>
<<set $cameOutAce to true>>
===
title: dani_out
position: 463,336
---
Dani: Woah! Hey! I wasn't laughing at you!
Dani: I'm laughing because-
Dani: I'm Asexual too!
Marji: What.
Dani: Yeah! I have an asexual pride flag in my room!
Marji: I'm not allowed in your room...
Dani: I mean yeah-- but
Marji: Why didn't you tell me?!?
(dani sprite shrugs)
Dani: Because you never asked.
Marji: OH. MY. GOD.
Dani: Oh! By the way, this doen't change anything! You're still not allowed in my room!
Marji: Don't care! Smells like chickens!
Dani: No it doesn't!
Marji: Does too!
Dani: Hey Sis?
<<jump the_end>>
===
title: taunt_bro
position: -37,42
---
Marji: I'm tellin' ya anyway!
<<profile GameStateMachine walker grimace>> 
Marji: Whatcha gonna do about it? walk away?!?
<<profile GameStateMachine walker default>>
Dani: Fine. You win this round sister. What is it?
-> I'm Asexual!
    <<jump comming_out>>
-> I don't like people the way others do
    <<jump comming_outt>>
===
title: throw_pillow
position: -266,254
---
<<PlayAndWaitCutscene GameStateMachine 0 true>>
Dani: okay! okay! It was a joke...
<<profile GameStateMachine marji default>> 
Marji: Damn right it was!
Marji: *Sigh* So, uh--
-> I'm Asexual!
    <<jump comming_out>>
-> I don't like people the way others do
    <<jump comming_outt>>
===
title: Gleeb
position: -454,50
---
Dani: The Midwest Guide to indentifying Passerines
Marji: Is that a bird book or...
Dani: Yes, I had a slice of pizza stolen yesterday and I need to know who did it...
Marji: Yeah, yeah. Cowabummer.
<<set $birdBook to true>>
=== 
